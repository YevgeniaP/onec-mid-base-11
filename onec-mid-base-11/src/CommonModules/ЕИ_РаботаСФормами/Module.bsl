#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
		
	Если Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма);
		ДобавитьГруппуСкидкаПересчет(Форма);
	ИначеЕсли Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма);
	ИначеЕсли Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма); 
	ИначеЕсли Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьКонтактноеЛицоПередОснование(Форма);
	ИначеЕсли Форма.ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьКонтактноеЛицоПередСуммаДокумента(Форма);
	КонецЕсли;

КонецПроцедуры 
	
#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ЕИ_КонтактноеЛицо"; 
	
КонецПроцедуры   

Процедура ВставитьКонтактноеЛицоПередОснование(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ЕИ_КонтактноеЛицо"; 
	
КонецПроцедуры

Процедура ВставитьКонтактноеЛицоПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ЕИ_КонтактноеЛицо"; 
	
КонецПроцедуры

Процедура ДобавитьГруппуСкидкаПересчет(Форма) 
	
	Группа = Форма.Элементы.Добавить("ГруппаСкидкаПересчет", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаПересчет);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ЕИ_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ЕИ_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитать = Форма.Команды.Добавить("ЕИ_ПересчитатьСУчетомСкидки");
	КомандаПересчитать.Заголовок = "Пересчитать с учетом скидки";
	КомандаПересчитать.Действие = "ЕИ_ПересчитатьСУчетомСкидки";
	
	КнопкаКоманды = Форма.Элементы.Добавить("ЕИ_ПересчитатьСУчетомСкидки", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидкаПересчет); 
	КнопкаКоманды.ИмяКоманды = "ЕИ_ПересчитатьСУчетомСкидки";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

#КонецОбласти     
